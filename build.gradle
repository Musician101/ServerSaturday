plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.1.0'
}

group = 'com.campmongoose'
version = '4.1'

repositories {
    mavenCentral()
    maven {
        name = 'papermc-repo'
        url = 'https://papermc.io/repo/repository/maven-public/'
    }
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
    maven {
        name = 'Minecraft'
        url = 'https://libraries.minecraft.net/'
    }
    mavenLocal()
}

dependencies {
    compileOnly 'io.papermc.paper:paper-api:1.18-R0.1-SNAPSHOT'
    implementation('io.musician101.musicianlibrary:spigot:4.2') {
        exclude group: 'org.spigotmc'
    }
    implementation 'io.musician101:bukkitier:1.0'
    implementation 'com.mojang:brigadier:1.0.18'
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

compileJava {
    def referenceName = 'Reference.java'
    def referencePath = "src/main/java/com/campmongoose/serversaturday/${referenceName}"
    def tempDir = 'build/tmp/sourcesCache'
    doFirst {
        copy {
            from referencePath
            into tempDir
        }

        ant.replace(file: referencePath, token: '@VERSION@', value: project.version)
    }

    doLast {
        copy {
            from "${tempDir}/${referenceName}"
            into project.file(referencePath).parent
        }
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

shadowJar {
    dependencies {
        include dependency('io.musician101:')
        include dependency('io.musician101.musicianlibrary:')
    }

    archiveFileName = "serversaturday-${project.version}.jar"
    relocate 'io.musician101.bukkitier', 'com.campmongoose.serversaturday.lib.io.musician101.bukkitier'
    relocate 'io.musician101.musicianlibrary', 'com.campmongoose.serversaturday.lib.io.musician101.musicianlibrary'
    dependsOn build
}
