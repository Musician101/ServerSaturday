buildscript {
    repositories {
        mavenCentral()
        jcenter()
        maven {
            name = 'gradle-plugins'
            url = 'https://plugins.gradle.org/m2'
        }
        maven {
            name = 'sponge'
            url = 'https://repo.spongepowered.org/maven'
        }
        maven {
            name = 'forge'
            url = 'http://files.minecraftforge.net/maven'
        }
    }

    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
        classpath 'org.spongepowered:spongegradle:0.8.1'
        classpath 'org.spongepowered:mixingradle:0.4-SNAPSHOT'
        classpath 'gradle.plugin.net.minecrell:vanillagradle:2.2-6'
    }
}

apply plugin: 'maven'
apply plugin: 'net.minecrell.vanillagradle.server'
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'org.spongepowered.plugin'

group project.pluginGroup
version project.pluginVersion

deobfMcMCP { failOnAtError = false }
deobfMcSRG { failOnAtError = false }

minecraft {
    version = "1.12.2"
    mappings = "stable_39"
}

repositories {
    maven {
        name = 'sponge'
        url = 'https://repo.spongepowered.org/maven/'
    }
}

configurations {
    shade
    compile.extendsFrom shade
}

dependencies {
    shade(project(':serversaturday-common')) {
        transitive = false
    }
    compile('org.spongepowered:spongeapi:7.3.0-SNAPSHOT')
    compile('org.spongepowered:mixin:0.7.1-SNAPSHOT') {
        exclude module: 'launchwrapper'
    }
}

mixin {
    add sourceSets.main, "mixins.sponge_server_saturday.json"
}

sourceSets {
    main {
        refMap = "mixins.sponge_server_saturday.refmap.json"
    }
}

jar {
    manifest.attributes(
            "TweakClass": "org.spongepowered.asm.launch.MixinTweaker",
            "MixinConfigs": "mixins.sponge_server_saturday.json",
            "FMLCorePluginContainsFMLMod": "true"
    )

    configurations.shade.each {
        from(it.isDirectory() ? it : project.zipTree(it)) {
            exclude "META-INF/**"
        }
    }
}
